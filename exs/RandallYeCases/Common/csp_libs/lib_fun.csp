-- lib_fun.csp

-- function means there's no element which can map to two different values
fun(X, Y) = { s | s<-rel(X, Y), empty({x1 | (x1,y1)<-s, (x2,y2)<-s, x1 == x2 and y1 != y2})}

-- partial function: not every element in X has the corresponding value in Y
-- implement 1: by its definition
-- pfun(X, Y) = { s | s<-rel(X,Y), empty({x1 | (x1,y1)<-s, (x2,y2)<-s, x1 == x2 and y1 != y2})}

-- implement 2: efficient 
----------------------------------- pfun [START] ----------------------------------------
-- a partial function pfun(X, Y) finally is a set of (set of pairs)
-- {{(x1,y1), (x2, y2) ... }, {(x1, y2)...}, {()}}
-- in its subset, the source of each pair (X) shall not be unique, therefore 
-- 	{(x1, y1), (x1, y2)} is not allowed
-- however the destination of each pair (y) don't have to be unique, therefore
--	{(x1, y1), (x2, y2)} is valid
--
-- the algorithms to calculate a partial function of X and Y, shown below
-- 1. generally it is a union of 
--	a) taking zero item from X, => {}
--	b) taking one item from X, => {{(x1,y1)}, {(x1,y2)}, ... {(x2,y1)}, ...}
--	c) taking two items from X, => {{(x1,y1), (x2, y1)}, ...}
--	i) ...
--	n) taking n items from X, => {{(x1,y1), (x2, y1), ..., (xn, y1)}, ...}
-- 2. for each small step a), b), ..., n), use pcomb(X, Y, n)
--	pcomb(X, Y, n) is used to compute all possible pairs which size is n.
--	for example, pcomp({1,2},{3},1) gets {{(1,3)},{(2,3)}} and the cardinality is one
-- 3. for pcomp,
--	a) at first, calculate all possible subset of X and its cardinality is n (by select2)
--	for example, select2({1,2,3}, 2) = {<1,2>,<1,3>,<2,3>}
--	b) then calculate all possible combination of elements from Y and its size is n as well.
--	for example, select1({1,2}, 2) = {<1,1>, <1,2>, <2,1>, <2,2>}
--	c) join the sequences from select2 and select1 together to get pcomp

-- select n items from x, order matters and duplicate items are allowed
-- select1({1,2}, 2) = {<1,1>, <1,2>, <2,1>, <2,2>}
select1(Y, 0) = { <> }
select1(Y, 1) = { <y> | y <- Y}
select1(Y, n) = { <y>^ss | y <- Y, ss <- select1(Y, n-1) }

-- select n items from x, order doesn't matter and duplicate items are not allowed
-- select({1,2}, 1) = {<1>,<2>}
-- select({1,2}, 2) = {<1,2>}
select2_1(X, 0) = {}
-- choose more than 1 from <x> leads to emptyset
select2_1(<x>, n) = if n > 1 then {} else { <x> }
select2_1(X, 1) = { <x> | x <- set(X) }
select2_1(<x>^s, n) = union({ <x> ^ ss | ss <- select2_1(s, n-1)}, select2_1(s, n))

select2(X, n) = select2_1(seq(X), n)

-- join(s1, s2) to form a {(s11, s21), (s12, s22), ..., (s1n, s2n)}
-- s1 and s2 are the same size sequences
-- s1 = <s11, s12, ..., s1n>
-- s2 = <s21, s22, ..., s2n>
pjoin(<x>, <y>) = {(x,y)}
pjoin(<x>^s1, <y>^s2) = union({(x,y)}, pjoin(s1, s2))

-- 
pcomb(X, Y, n) = { pjoin(s1, s2) | s1 <- select2(X, n), s2 <- select1(Y, n)}

pfun1(X, Y, 1) = {{(x,y)} | x <- X, y <- Y}
pfun1(X, Y, n) = union(pfun1(X, Y, n-1), pcomb(X, Y, n))

pfun(X, Y) = union(pfun1(X, Y, card(X)), {{}})
----------------------------------- pfun [END] ---------------------------------------

-- total function: every element in X has the corresponding value in Y
-- implementation 1:
-- tfun(X, Y) = { s | s<-pfun(X, Y), dom(s) == X}

-- implementation 2:
tfun1(<>, Y) = {}
--tfun(X, {}) = {}
tfun1(<x>, Y) = {{(x, y)} | y <- Y}
tfun1(<x>^s, Y) = { union(sx, ss) | sx <- tfun1(<x>, Y), ss <- tfun1(s, Y)}

tfun(X, Y) = tfun1(seq(X), Y)

-- Partial injections: every value in Y is mapped to up to one element in X
pifun(X, Y) = { s | s<-pfun(X, Y), empty({x1 | (x1,y1)<-s, (x2,y2)<-s, y1 == y2 and x1 != x2})}

-- Total injections: Partial injections and total function
tifun(X, Y) = inter(pifun(X, Y), tfun(X, Y))

-- Partial surjections: every value in Y is mapped to at least one element in X
psfun(X, Y) = { s | s<-pfun(X, Y), ran(s) == Y}

-- Total surjections: Partial surjections and total function
tsfun(X, Y) = inter(psfun(X, Y), tfun(X, Y))

-- Bijections: total surjections and total injections
bjfun(X, Y) = inter(tsfun(X, Y), tifun(X, Y))

-- function application: f(x)
-- fa(f, x)
-- fa(f)(x) is not supported in ProB
fa(f, a) = 
    let
        pick({x}) = x
        y = pick({yy | (x, yy)<-f, x == a})
    within y
